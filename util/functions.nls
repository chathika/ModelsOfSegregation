;; Function that determines how agents evaulates location

;; The turtle calculates the fraction of friends in the neighborhood of a given patch
;; turtle procedure
;; @EMD @factor @return-type=float
to-report calc-fraction-of-friends
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let my-group color-group
  let occupied-neighbor-patches neighbor-patches with [any? turtles-here]
  let no-of-friends count occupied-neighbor-patches with [[color-group] of resident = my-group]
  let no-of-neighbors count occupied-neighbor-patches
  report ifelse-value (no-of-neighbors > 0)[no-of-friends / no-of-neighbors][0]
end

;; turtle procedure
;; reports the normalized variance in tolerance in the patch neighborhood
;; @EMD @factor @return-type=float
to-report variance-neighborhood-tolerance
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let tolerances [[tolerance] of resident] of neighbor-patches with [any? turtles-here]
  report ifelse-value (length tolerances > 1) [variance tolerances] [0]
end

;; turtle procedure
;; reports the mean tolerance in the patch neighborhood
;; @EMD @factor @return-type=float
to-report mean-neighborhood-tolerance
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let tolerances [[tolerance] of resident] of neighbor-patches with [any? turtles-here]
  report ifelse-value (length tolerances > 0) [mean tolerances] [0]
end

;; turtles procedure
;; reports the normalized isolation of the patch neighborhood
;; @EMD @factor @return-type=float
to-report neighborhood-isolation
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let no-of-isolated-patches count neighbor-patches with [resident = nobody]
  report no-of-isolated-patches / count neighbor-patches
end

;; reports the mean of the home-utility experienced by residents in this neighborhood
;; @EMD @factor @return-type=float
to-report mean-home-utility-of-neighborhood
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let home-utilities [[home-utility] of resident] of neighbor-patches with [any? turtles-here]
  report ifelse-value (length home-utilities > 0) [mean home-utilities] [0]
end

;; reports the variance of the home-utility experienced by residents in this neighborhood
;; @EMD @factor @return-type=float
to-report variance-home-utility-of-neighborhood
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let home-utilities [[home-utility] of resident] of neighbor-patches with [any? turtles-here]
  report ifelse-value (length home-utilities > 1) [variance home-utilities] [0]
end

;; reports the distance from the home-patch
;; turtle procedure
;; @EMD @factor @return-type=float
to-report distance-from-home-patch
  report [distance patch-being-evaluated] of home-patch / max-distance-between-patches
end

;; reports number of moves / ticks if patch-being-evaluated is not home-patch
;; reports 1-(moves/ticks) if patch-being-evaluated is home-patch
;; turtle procedure
;; @EMD @factor @return-type=float
to-report my-tendency-to-move
  if ticks = 0 [report 0]
  ifelse patch-being-evaluated = home-patch [
    report 1 - (sum my-recent-moves) / max-history-length
  ][
    report (sum my-recent-moves) / max-history-length
  ]
end

;; reports the mean neighbor age here
;; turtle procedure
;; @EMD @factor @return-type=float
to-report mean-neighborhood-age
  if ticks = 0 [report 0]
  let neighbor-patches [neighboring-patches] of patch-being-evaluated
  let neighborhood-ages [[ticks-at-current-residence] of resident] of neighbor-patches with [any? turtles-here]
  report ifelse-value length neighborhood-ages > 0 [mean neighborhood-ages /( 1 + max-ticks-at-current-residence)][0]
end


;;;;;; operators ;;;;;;
;; geometric mean
;; @EMD @operator @return-type=float @parameter-type=float @parameter-type=float @structure=+,+
to-report mean_ [a b]
  report (a + b) / 2
end

;; run by a patch
;; flips subutility around 0.5. sub-utility range is between 0, 1
;; @EMD @operator @return-type=float @parameter-type=float @structure=-
to-report invert [a]
  report ((a - 0.5) * -1) + 0.5
end
